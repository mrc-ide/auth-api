buildscript {
    ext.kotlin_version = '1.2.50'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
}

group 'org.mrc.ide'
version '1.0-SNAPSHOT'

task generateDatabaseInterface(dependsOn: ':generateDatabaseInterface:run')

task startDatabase(type: Exec) {
    doFirst {
        environment([
                'DB_VERSION': "master",
                'DB_PORT': "5432"
        ])
    }
    commandLine 'bash', '../scripts/start-database.sh'
}


task stopDatabase(type: Exec) {
    commandLine 'bash', '../scripts/stop-database.sh'
}

subprojects {
    apply plugin: 'kotlin'

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven {
            url "https://raw.githubusercontent.com/mrc-ide/serialization/mvn-repo/"
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        compile "org.slf4j:slf4j-simple:1.7.25"
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}
